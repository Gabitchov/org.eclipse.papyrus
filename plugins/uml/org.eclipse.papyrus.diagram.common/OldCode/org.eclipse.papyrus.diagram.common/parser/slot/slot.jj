options {
  JAVA_UNICODE_ESCAPE = true;
  STATIC=false;
}

PARSER_BEGIN(SlotParser)

/*
 * Copyright (c) 2006 Borland Software Corporation
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Michael Golubev (Borland) - initial API and implementation
 */
package org.eclipse.uml2.diagram.clazz.parser.slot;

import java.io.*;
import org.eclipse.emf.ecore.EClass;
import org.eclipse.emf.ecore.EObject;
import org.eclipse.papyrus.diagram.common.parser.*;
import org.eclipse.papyrus.diagram.common.parser.lookup.LookupSuite;
import org.eclipse.uml2.uml.*;

public class SlotParser extends ExternalParserBase {
	private Slot mySubject;
	private Type myOptionalType;
	
    public SlotParser(){
    	this(new StringReader(""));
    }
    
    public SlotParser(LookupSuite lookup){
    	this();
    	setLookupSuite(lookup);
    }

	public EClass getSubjectClass(){
		return UMLPackage.eINSTANCE.getSlot();
	}
	
	public void parse(EObject target, String text) throws ExternalParserException {
		checkContext();
		myOptionalType = null;
		ReInit(new StringReader(text));
		mySubject = (Slot)target;
		Declaration();
		mySubject = null;
		myOptionalType = null;
	}
	
	protected static int parseInt(Token t) throws ParseException {
		if (t.kind != SlotParserConstants.INTEGER_LITERAL){
			throw new IllegalStateException("Token: " + t + ", image: " + t.image);
		}
		try {
			return Integer.parseInt(t.image); //XXX: "0005", "99999999999999999999999"
		} catch (NumberFormatException e){
			throw new ParseException("Not supported integer value:" + t.image);
		}
	}
	
}

PARSER_END(SlotParser)

/* WHITE SPACE */

SPECIAL_TOKEN :
{
  " "
| "\t"
}

/* SEPARATORS */
TOKEN :
{
	< SLASH: "/" >
|	< COLON: ":" >
|	< EQUALS: "=" >
|	< LBRACKET: "[" >
|	< RBRACKET: "]" >
|	< LCURLY: "{" >
|	< RCURLY: "}" >
|	< COMMA: "," >
}

/* SPECIAL_MEANING */
TOKEN :
{
	< PLUS: "+" >
|	< MINUS: "-" >
|	< NUMBER_SIGN: "#" >
|	< TILDE: "~" >
|	< DOT: "." >
|	< STAR: "*" >
}

/* MODIFIERS */
TOKEN :
{
	< READ_ONLY: "readOnly" >
|	< UNION: "union" >
|	< SUBSETS: "subsets" >
|	< REDEFINES: "redefines" >
|	< ORDERED: "ordered" >
|	< UNORDERED: "unordered" > 
|	< UNIQUE: "unique" >
|	< NON_UNIQUE: "nonunique" >
}
	
/* LITERALS */
TOKEN: 
{
	< INTEGER_LITERAL: ["0"-"9"] (["0"-"9"])* >
}
  
TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

void Declaration() :
{}
{
	(
		[ SlotFeatureName() ]
		[ SlotFeatureType() ]
		[ SlotValue() ]
	) <EOF>
}

void SlotFeatureName() :
{
	String name;
}
{
	name = NameWithSpaces() 
	{
		StructuralFeature feature = lookup(StructuralFeature.class, name);
		if (feature != null){
			mySubject.setDefiningFeature(feature);
		}
	}
}

void SlotFeatureType() :
{
	//we do not want to modify feature type when slot type is changed
	//however, we do want to use provided type to construct correct value
	//Thus we are going to cache type here and use it in the SlotValue()
	String type;
}
{
	<COLON> type = NameWithSpaces() { myOptionalType = lookup(Type.class, type); }
}


String NameWithSpaces() :
{
	StringBuffer result = new StringBuffer();
	Token t;
}
{
	(
		t = <IDENTIFIER> { result.append(t.image); } 
		( t = <IDENTIFIER> { result.append(' '); result.append(t.image); } ) *
	)
	{
		return result.toString();
	}
}

/**
 * FIXME: Actually only integers and strings are supported. 
 * Expression will be used as escape if value is neither string nor integer literal
 */
void SlotValue() :
{
	Token t;
	String text;	
}
{
	(
		<EQUALS> 
		(
			"\"" text = NameWithSpaces() "\"" 
			{ 
				LiteralString literalString = (LiteralString)mySubject.createValue(null, myOptionalType, UMLPackage.eINSTANCE.getLiteralString());
				literalString.setValue(text);
			}
		|
			text = NameWithSpaces()
			{
				Expression expression = (Expression)mySubject.createValue(null, myOptionalType, UMLPackage.eINSTANCE.getExpression());
				expression.setSymbol(text);
			}
		| 
			t = <INTEGER_LITERAL> 
			{ 
				int value = parseInt(t);
				LiteralInteger literalInteger = (LiteralInteger)mySubject.createValue(null, myOptionalType, UMLPackage.eINSTANCE.getLiteralInteger());
				literalInteger.setValue(value);
			} 
		)
	)
}
