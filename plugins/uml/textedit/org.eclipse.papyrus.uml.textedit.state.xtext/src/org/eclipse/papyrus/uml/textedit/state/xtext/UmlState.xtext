/*****************************************************************************
 * Copyright (c) 2010 CEA LIST.
 *
 *    
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *  CEA LIST - Initial API and implementation
 *
 *****************************************************************************/

grammar org.eclipse.papyrus.uml.textedit.state.xtext.UmlState with org.eclipse.papyrus.uml.alf.Common

import "http://www.eclipse.org/uml2/4.0.0/UML" as uml

generate umlState "http://www.eclipse.org/papyrus/uml/textedit/state/xtext/UmlState"

StateRule :
	name = ID 
	(':' submachine = SubmachineRule)?
	((entry = EntryRule)? &
	(do = DoRule)?  &
	(exit = ExitRule)? );

SubmachineRule :
	(path=QualifiedName)? submachine=[uml::StateMachine] ;

QualifiedName :
	path=[uml::Namespace]'::' (remaining=QualifiedName)? ; 
	
EntryRule :
	"entry"
	kind = BehaviorKind 
	behaviorName = ID ;

DoRule :
	"do" 
	kind = BehaviorKind 
	behaviorName = ID ;
	
ExitRule :
	"exit" 
	kind = BehaviorKind 
	behaviorName = ID ;

enum BehaviorKind :
	ACTIVITY = "Activity" |
	STATE_MACHINE = "StateMachine" |
	OPAQUE_BEHAVIOR = "OpaqueBehavior" ;