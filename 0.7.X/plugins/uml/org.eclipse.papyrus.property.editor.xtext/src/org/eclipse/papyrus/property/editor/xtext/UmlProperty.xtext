/*****************************************************************************
 * Copyright (c) 2010 CEA LIST.
 *
 *    
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *  CEA LIST - Initial API and implementation
 *
 *****************************************************************************/

grammar org.eclipse.papyrus.property.editor.xtext.UmlProperty with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/uml2/3.0.0/UML" as uml

generate umlProperty "http://www.eclipse.org/papyrus/property/editor/xtext/UmlProperty"

PropertyRule:
	visibility = VisibilityKind
	(isDerived = "/")? 
	name=ID ':' ((type=[uml::Classifier]) | "<Undefined>")
	(multiplicity = MultiplicityRule) ? 
	(modifiers = ModifiersRule) ? ;

enum VisibilityKind:
	public = "+"
	| private = "-"
	| protected = "#"
	| package = "~" ;
	
MultiplicityRule:
	"[" bounds+=BoundSpecification ('..' bounds+=BoundSpecification)? "]" ;

BoundSpecification:
	value = UnlimitedLiteral ;

terminal UnlimitedLiteral:
	'0'..'9' ('0'..'9')* | "*" ;

ModifiersRule:
	'{' values += ModifierSpecification (',' values += ModifierSpecification)* '}';

ModifierSpecification:
	value = ModifierKind ;

enum ModifierKind:
	readOnly = "readOnly" 
	| union = "union" 
	| ordered = "ordered" 
	| unique = "unique" ;
	